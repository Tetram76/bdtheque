; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Languages]
Name: fr; MessagesFile: compiler:Languages\French.isl

[Setup]
AppVersion={#GetFileVersion("D:\Bureautique\BDthèque\BD.exe")}
AppName=BDthèque
AppVerName=BDthèque {#SetupSetting("AppVersion")}
AppMutex=TetramCorpBDMutex
AppPublisher=Teträm Corp
AppPublisherURL=http://www.tetram.org
AppSupportURL=mailto:dev@tetram.org
AppUpdatesURL=http://www.tetram.org
UninstallDisplayIcon={app}\BD.exe
DefaultDirName={pf}\Teträm Corp\BDthèque
DefaultGroupName=Teträm Corp\BDthèque
PrivilegesRequired=poweruser
AppCopyright=Teträm Corp © 1997-2004
AppID={{A86E29B5-D1EE-431F-A5BF-E4A10D36CBDC}
LicenseFile=G:\Programmation\MEDIA.KIT\LicenceFreeWare.rtf
WindowVisible=false
BackColor=$8080ff
BackColor2=clPurple
BackColorDirection=toptobottom
WizardImageBackColor=clBlack
OutputBaseFilename=BDTheque-setup-{#SetupSetting("AppVersion")}
MinVersion=4.1.1998,5.0.2195
EnableDirDoesntExistWarning=true
AllowUNCPath=false
AppendDefaultDirName=false
ShowLanguageDialog=yes
InternalCompressLevel=normal
SolidCompression=true
Compression=lzma/normal
VersionInfoVersion={#SetupSetting("AppVersion")}
VersionInfoCompany=Teträm Corp
VersionInfoTextVersion={#SetupSetting("AppVersion")}
WizardImageFile=fond.bmp
WizardSmallImageFile=SetupModernSmall19.bmp

[Tasks]
Name: desktopicon; Description: Créer un raccourci sur le &bureau; GroupDescription: Raccourcis supplémentaires:
Name: interneticon; Description: Créer un raccourci vers le site de Teträm Corp; GroupDescription: Raccourcis supplémentaires:

[Files]
Source: D:\Bureautique\BDthèque\BD.exe; DestDir: {app}
Source: D:\Bureautique\BDthèque\BDPic.dll; DestDir: {app}; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\fbembed.dll; DestDir: {app}
Source: D:\Bureautique\BDthèque\firebird.msg; DestDir: {app}; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\ib_util.dll; DestDir: {app}
Source: D:\Bureautique\BDthèque\UDF\BDT_UDF.dll; DestDir: {app}\UDF
Source: Base vide\BD.GDB; DestDir: {app}; Flags: onlyifdoesntexist ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Acceuil.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Fond.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\RepInitiales.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Repertoire.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\FicheAlbum.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\FichePersonne.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\FicheSerie.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Manquants.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Previsions.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Styles.css; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\graphics\Acceuil.jpg; DestDir: {app}\WebServer\graphics; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\graphics\logo.jpg; DestDir: {app}\WebServer\graphics; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\graphics\fondbdd.jpg; DestDir: {app}\WebServer\graphics; Flags: ignoreversion
Source: What's New.txt; DestDir: {app}; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[INI]
Filename: {app}\BD.url; Section: InternetShortcut; Key: URL; String: http://www.tetram.org; Tasks: interneticon

[Icons]
Name: {group}\BDthèque; Filename: {app}\BD.exe; IconIndex: 0
Name: {group}\BDthèque on the Web; Filename: {app}\BD.url; Tasks: interneticon
Name: {group}\Désinstaller BDthèque; Filename: {uninstallexe}
Name: {userdesktop}\BDthèque; Filename: {app}\BD.exe; Tasks: desktopicon; IconIndex: 0

[Run]
Filename: {app}\BD.exe; Description: Lancer BDthèque; Flags: nowait postinstall skipifsilent
Filename: {app}\What's New.txt; Flags: nowait shellexec skipifdoesntexist postinstall skipifsilent

[UninstallDelete]
Type: files; Name: {app}\BD.url
Type: files; Name: {app}\BD.ini
Type: files; Name: {app}\*.lck
Type: files; Name: {app}\firebird.log

[LangOptions]
LanguageName=French
LanguageID=$040C

[_ISTool]
UseAbsolutePaths=false

[InstallDelete]
Name: {app}\UDF\VDO_UDF.dll; Type: files
Name: {app}\BDws.dll; Type: files

[CustomMessages]
CustomFormCaption=BDthèque est déjà installé
CustomFormDescription=Le programme d'installation a détecté que BDthèque est déjà installé sur cet ordinateur.
UninstallRegKey=SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{A86E29B5-D1EE-431F-A5BF-E4A10D36CBDC}_is1

[Code]
  var
    CanUpdate, IsUpdate: Boolean;
    Label2: TLabel;
    RadioButton1: TRadioButton;
    RadioButton2: TRadioButton;

  procedure CustomForm_Activate(Page: TWizardPage);
  begin
  end;

  function CustomForm_ShouldSkipPage(Page: TWizardPage): Boolean;
  begin
    Result := False;
  end;

  function CustomForm_BackButtonClick(Page: TWizardPage): Boolean;
  begin
    Result := True;
  end;

  function CustomForm_NextButtonClick(Page: TWizardPage): Boolean;
  begin
    Result := True;
    IsUpdate := RadioButton2.Checked;
  end;

  procedure CustomForm_CancelButtonClick(Page: TWizardPage; var Cancel, Confirm: Boolean);
  begin
  end;

  function CustomForm_CreatePage(PreviousPageId: Integer): Integer;
  var
    Page: TWizardPage;
  begin
    Page := CreateCustomPage(
      PreviousPageId,
      ExpandConstant('{cm:CustomFormCaption}'),
      ExpandConstant('{cm:CustomFormDescription}')
    );

    { Label2 }
    Label2 := TLabel.Create(Page);
    with Label2 do
    begin
      Parent := Page.Surface;
      Left := ScaleX(24);
      Top := ScaleY(40);
      Width := ScaleX(366);
      Height := ScaleY(58);
      AutoSize := False;
      Caption := 'Selectionnez l''action que vous voulez effectuer et cliquer sur Suivant pour continuer.';
      WordWrap := True;
    end;

    { RadioButton1 }
    RadioButton1 := TRadioButton.Create(Page);
    with RadioButton1 do
    begin
      Parent := Page.Surface;
      Left := ScaleX(120);
      Top := ScaleY(136);
      Width := ScaleX(113);
      Height := ScaleY(17);
      Caption := 'Installer de nouveau';
      TabOrder := 0;
    end;

    { RadioButton2 }
    RadioButton2 := TRadioButton.Create(Page);
    with RadioButton2 do
    begin
      Parent := Page.Surface;
      Left := ScaleX(120);
      Top := ScaleY(112);
      Width := ScaleX(113);
      Height := ScaleY(17);
      Caption := 'Mettre à jour';
      Checked := True;
      TabOrder := 1;
      TabStop := True;
    end;


    with Page do
    begin
      OnActivate := @CustomForm_Activate;
      OnNextButtonClick := @CustomForm_NextButtonClick;
    end;

    Result := Page.ID;
  end;

  procedure InitializeWizard();
  var
    CurVersion: string;
  begin
    IsUpdate := False;
    CanUpdate := RegQueryStringValue(HKLM, ExpandConstant('{cm:UninstallRegKey}'), 'DisplayVersion', CurVersion) and (CurVersion <> '');
    if CanUpdate then
      CustomForm_CreatePage(wpLicense);
  end;

  function ShouldSkipPage(PageID: Integer): Boolean;
  begin
    Result := ((PageID = wpSelectDir) or (PageID = wpSelectProgramGroup)) and CanUpdate and IsUpdate;
  end;
